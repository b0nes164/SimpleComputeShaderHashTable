#pragma kernel Initialize
#pragma kernel Insert
#pragma kernel Lookup
#pragma kernel Delete

#define THREAD_BLOCKS 256
#define GROUP_SIZE 64
#define EMPTY 0xffffffff

RWStructuredBuffer<uint2> b_hash;
RWStructuredBuffer<uint> b_inputOutput;

extern uint e_hashBufferSize;
extern uint e_inputSize;

//********************************
// Murmurhash3 32Bit Hash Function
// Author: Austin Appleby
// Lastmod: 1/8/2016
// Liscence: MIT License
// Added 8/4/2021
uint hash(uint k, uint bufferSize)
{
    k ^= k >> 16;
    k *= 0x85ebca6b;
    k ^= k >> 13;
    k *= 0xc2b2ae35;
    k ^= k >> 16;
    return k & (bufferSize - 1);
}

void HashInsert(RWStructuredBuffer<uint2> hashBuffer, uint key, uint value, uint bufferSize)
{
    uint slot = hash(key, bufferSize);
    while (true)
    {
        uint prev;
        InterlockedCompareExchange(hashBuffer[slot].x, EMPTY, key, prev);
        if (prev == EMPTY || prev == key)
        {
            hashBuffer[slot].y = value;
            return;
        }
        slot = (slot + 1) & (bufferSize - 1);
    }
}

uint HashLookup(RWStructuredBuffer<uint2> hashBuffer, uint key, uint bufferSize)
{
    uint slot = hash(key, bufferSize);
    while (true)
    {
        if (hashBuffer[slot].x == key)
        {
            return hashBuffer[slot].y;
        }
        if (hashBuffer[slot].x == EMPTY)
        {
            return EMPTY;
        }
        slot = (slot + 1) & (bufferSize - 1);
    }
    return EMPTY;
}

void HashDelete(RWStructuredBuffer<uint2> hashBuffer, uint key, uint bufferSize)
{
    uint slot = hash(key, bufferSize);
    while (true)
    {
        if (hashBuffer[slot].x == EMPTY)
        {
            return;
        }
        if (hashBuffer[slot].x == key)
        {
            hashBuffer[slot].y = EMPTY;
            return;
        }
        slot = (slot + 1) & (bufferSize - 1);
    }
}

//In order for the Hash Table to work, it must be initialized to the empty sentinel value
[numthreads(GROUP_SIZE, 1, 1)]
void Initialize(uint3 id : SV_DispatchThreadID)
{
    for (uint i = id.x; i < e_hashBufferSize; i += GROUP_SIZE * THREAD_BLOCKS)
    {
        b_hash[i] = uint2(EMPTY, EMPTY);
    }
}

[numthreads(GROUP_SIZE, 1, 1)]
void Insert(uint3 id : SV_DispatchThreadID)
{
    for (uint i = id.x; i < e_inputSize; i += GROUP_SIZE * THREAD_BLOCKS)
    {
        HashInsert(b_hash, i, b_inputOutput[i], e_hashBufferSize);
    }
}

[numthreads(GROUP_SIZE, 1, 1)]
void Lookup(uint3 id : SV_DispatchThreadID)
{
    for (uint i = id.x; i < e_inputSize; i += GROUP_SIZE * THREAD_BLOCKS)
    {
        b_inputOutput[i] = HashLookup(b_hash, i, e_hashBufferSize);
    }
}

[numthreads(GROUP_SIZE, 1, 1)]
void Delete(uint3 id : SV_DispatchThreadID)
{
    for (uint i = id.x; i < e_inputSize; i += GROUP_SIZE * THREAD_BLOCKS)
    {
        HashDelete(b_hash, i, e_hashBufferSize);
    }
}